+class DocArWnd(\doc_ar@DocAr)
	*func ctor()
		do super(me)
		do me.root :: #@ObjWnd
		var width: int
		var height: int
		do me.root.getDefaultSize(&width, &height)
		do me.root.init("wndMain", true, 0, 0, width, height, null)
	end func
	
	+*func fix(first: bool)
		var lines: list<[]char> :: #list<[]char>
		do lines.add("+var \{me.root.name}: \{(me.root $ @ObjArWnd).type()}")
		
		do me.nodes.head()
		while(!me.nodes.term())
			var node: @ObjArWnd :: me.nodes.get() $ @ObjArWnd
			do lines.add("+var \{node.name}: \{node.type()}")
			do me.nodes.next()
		end while
		
		do lines.add("")
		do lines.add("+func show()")
		do lines.add("\tif(@\{me.root.name} <>& null)")
		do lines.add("\t\tret")
		do lines.add("\tend if")
		do(me.root $ @ObjArWnd).getSrcMake(1, lines, "null")
		if(!me.root.visible)
			do lines.add("\tdo @\{me.root.name}.setVisible(false)")
		end if
		
		var parentStack: stack<\doc_ar@Obj> :: #stack<\doc_ar@Obj>
		do me.nodes.head()
		while(!me.nodes.term())
			var obj: @ObjArWnd :: me.nodes.get() $ @ObjArWnd
			var parent: \doc_ar@Obj :: \doc_ar@getParent(parentStack, obj)
			if(parent =& null)
				do obj.getSrcMake(1, lines, "@" ~ me.root.name)
			else
				var x: int :: obj.x
				var y: int :: obj.y
				do obj.x :- parent.x
				do obj.y :- parent.y
				do obj.getSrcMake(1, lines, "@" ~ parent.name)
				do obj.x :: x
				do obj.y :: y
			end if
			if(obj.becomeParent())
				do parentStack.add(obj)
			end if
			if(!obj.visible)
				do lines.add("\tdo @\{obj.name}.setVisible(false)")
			end if
			do me.nodes.next()
		end while
		
		do lines.add("end func")
		do lines.add("")
		do lines.add("+func close()")
		do lines.add("\tif(@\{me.root.name} =& null)")
		do lines.add("\t\tret")
		do lines.add("\tend if")
		do lines.add("\tdo @\{me.root.name}.close()")
		do lines.add("\tdo @\{me.root.name} :: null")
		
		do me.nodes.head()
		while(!me.nodes.term())
			do lines.add("\tdo @\{me.nodes.get().name} :: null")
			do me.nodes.next()
		end while
		
		do lines.add("end func")
		do me.src :: lines.toArray()
	end func
	
	*func makeObj(name: []char): \doc_ar@Obj
		switch(name)
		case "Wnd"
			ret #@ObjWnd
		case "Btn"
			ret #@ObjBtn
		case "Edit"
			ret #@ObjEdit
		case "Label"
			ret #@ObjLabel
		case "Chk"
			ret #@ObjChk
		case "Group"
			ret #@ObjGroup
		case "EditMulti"
			ret #@ObjEditMulti
		end switch
		ret null
	end func
	
	*func updateLt()
		do \form@listLt.add("Btn", 1)
		do \form@listLt.add("Chk", 2)
		do \form@listLt.add("Edit", 3)
		do \form@listLt.add("Label", 4)
		do \form@listLt.add("Group", 5)
		do \form@listLt.add("EditMulti", 12)
	end func
end class

class ObjArWnd(\doc_ar@Obj)
	+func type(): []char
	end func
	
	+func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
	end func
	
	func addEvent(indent: int, lines: list<[]char>, name: []char)
		var value: []char :: me.props.get(name, &)
		if(value = "" | value = "null")
			ret
		end if
		do lines.add("\t".repeat(indent) ~ "do @\{me.name}.\{name} :: \{value}")
	end func
end class

class ObjWnd(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFF0F0F0)
		do draw@rect(x * zoom, (y - 30.0) * zoom, width * zoom, 30.0 * zoom, 0xFFFFFFFF)
		do draw@rectLine((x - 1.0) * zoom, (y - 31.0) * zoom, (width + 2.0) * zoom, (height + 32.0) * zoom, 0xFF1883D7)
		do \common@fontP.drawScale((x + 30.0) * zoom, (y - 30.0 + 6.0) * zoom, zoom, zoom, me.props.get("text", &), draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 50
		do minHeight :: 50
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 640
		do height :: 480
	end func
	
	+*func kind(): []char
		ret "Wnd"
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("style", "%normal")
		do me.props.add("text", me.name)
		do me.props.add("onClose", "")
		do me.props.add("onActivate", "")
		do me.props.add("onPushMenu", "")
		do me.props.add("onDropFiles", "")
		do me.props.add("onResize", "")
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["style", "text", "onClose", "onActivate", "onPushMenu", "onDropFiles", "onResize"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %str, %str, %str, %str, %str, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%normal", "%fix", "%aspect", "%popup"], null, null, null, null, null, null]
	end func
	
	+*func type(): []char
		ret "wnd@Wnd"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeWnd(\{parent}, \{me.props.get("style", &)}, \{me.width}, \{me.height}, \"\{me.props.get("text", &)}\")")
		do me.addEvent(indent, lines, "onClose")
		do me.addEvent(indent, lines, "onActivate")
		do me.addEvent(indent, lines, "onPushMenu")
		do me.addEvent(indent, lines, "onDropFiles")
		do me.addEvent(indent, lines, "onResize")
	end func
end class

class ObjBtn(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFE1E1E1)
		do draw@rectLine(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFADADAD)
		var text: []char :: me.props.get("text", &)
		do \common@fontP.drawScale(lib@floor(x + (width - \common@fontP.calcWidth(text)) / 2.0) * zoom, lib@floor(y + (height - \common@fontP.maxHeight()) / 2.0) * zoom, zoom, zoom, text, draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 23
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 75
		do height :: 23
	end func
	
	+*func kind(): []char
		ret "Btn"
	end func
	
	+*func icon(): int
		ret 0
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("text", me.name)
		do me.props.add("onPush", "")
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "text", "onPush"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %str, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null, null]
	end func
	
	+*func type(): []char
		ret "wnd@Btn"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeBtn(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)}, \"\{me.props.get("text", &)}\")")
		do me.addEvent(indent, lines, "onPush")
	end func
end class

class ObjEdit(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFFFFFFF)
		do draw@rectLine(x * zoom, y * zoom, width * zoom, height * zoom, 0xFF7A7A7A)
		var text: []char :: me.props.get("text", &)
		var rightAligned: bool :: me.props.get("rightAligned", &) <> "false"
		if(rightAligned)
			var textWidth: float :: \common@fontP.calcWidth(text)
			do \common@fontP.drawScale((x + width - 5.0 - textWidth) * zoom, (y + 3.0) * zoom, zoom, zoom, text, draw@black)
		else
			do \common@fontP.drawScale((x + 5.0) * zoom, (y + 3.0) * zoom, zoom, zoom, text, draw@black)
		end if
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 19
		do maxWidth :: lib@intMax
		do maxHeight :: 19
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 100
		do height :: 19
	end func
	
	+*func kind(): []char
		ret "Edit"
	end func
	
	+*func icon(): int
		ret 2
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("readonly", "false")
		do me.props.add("rightAligned", "false")
		do me.props.add("text", "")
		do me.props.add("onChange", "")
		do me.props.add("onFocus", "")
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "readonly", "rightAligned", "text", "onChange", "onFocus"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %bool_, %bool_, %str, %str, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null, null, null, null, null]
	end func
	
	+*func type(): []char
		ret "wnd@Edit"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeEdit(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)})")
		var readonly: []char :: me.props.get("readonly", &)
		if(readonly <> "false")
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.readonly(\{readonly})")
		end if
		var rightAligned: []char :: me.props.get("rightAligned", &)
		if(rightAligned <> "false")
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.rightAligned(\{rightAligned})")
		end if
		var text: []char :: me.props.get("text", &)
		if(^text > 0)
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.setText(\"\{text}\")")
		end if
		do me.addEvent(indent, lines, "onChange")
		do me.addEvent(indent, lines, "onFocus")
	end func
end class

class ObjEditMulti(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFFFFFFF)
		do draw@rectLine(x * zoom, y * zoom, width * zoom, height * zoom, 0xFF7A7A7A)
		var text: []char :: me.props.get("text", &)
		do \common@fontP.drawScale((x + 5.0) * zoom, (y + 3.0) * zoom, zoom, zoom, text, draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 19
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 100
		do height :: 19
	end func
	
	+*func kind(): []char
		ret "EditMulti"
	end func
	
	+*func icon(): int
		ret 11
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("readonly", "false")
		do me.props.add("text", "")
		do me.props.add("onChange", "")
		do me.props.add("onFocus", "")
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "readonly", "text", "onChange", "onFocus"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %bool_, %str, %str, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null, null, null, null]
	end func
	
	+*func type(): []char
		ret "wnd@EditMulti"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeEditMulti(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)})")
		var readonly: []char :: me.props.get("readonly", &)
		if(readonly <> "false")
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.readonly(\{readonly})")
		end if
		var text: []char :: me.props.get("text", &)
		if(^text > 0)
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.setText(\"\{text}\")")
		end if
		do me.addEvent(indent, lines, "onChange")
		do me.addEvent(indent, lines, "onFocus")
	end func
end class

class ObjLabel(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, y * zoom, width * zoom, height * zoom, 0xFFEEEEEE)
		var text: []char :: me.props.get("text", &)
		do \common@fontP.drawScale(x * zoom, y * zoom, zoom, zoom, text, draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 12
		do maxWidth :: lib@intMax
		do maxHeight :: 12
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 35
		do height :: 12
	end func
	
	+*func kind(): []char
		ret "Label"
	end func
	
	+*func icon(): int
		ret 3
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("text", me.name)
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "text"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null]
	end func
	
	+*func type(): []char
		ret "wnd@Label"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeLabel(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)}, \"\{me.props.get("text", &)}\")")
	end func
end class

class ObjChk(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		do draw@rect(x * zoom, lib@floor(y + (height - 13.0) / 2.0) * zoom, 13.0 * zoom, 13.0 * zoom, 0xFFFFFFFF)
		do draw@rectLine(x * zoom, lib@floor(y + (height - 13.0) / 2.0) * zoom, 13.0 * zoom, 13.0 * zoom, 0xFF333333)
		var text: []char :: me.props.get("text", &)
		do \common@fontP.drawScale((x + 18.0) * zoom, lib@floor(y + (height - \common@fontP.maxHeight()) / 2.0) * zoom, zoom, zoom, text, draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 16
		do minHeight :: 16
		do maxWidth :: lib@intMax
		do maxHeight :: 16
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 80
		do height :: 16
	end func
	
	+*func kind(): []char
		ret "Chk"
	end func
	
	+*func icon(): int
		ret 1
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("chk", "false")
		do me.props.add("text", me.name)
		do me.props.add("onPush", "")
		do me.reloadRes()
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "chk", "text", "onPush"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %bool_, %str, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null, null, null]
	end func
	
	+*func type(): []char
		ret "wnd@Chk"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeChk(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)}, \"\{me.props.get("text", &)}\")")
		var chk: []char :: me.props.get("chk", &)
		if(chk <> "false")
			do lines.add("\t".repeat(indent) ~ "do @\{me.name}.setChk(\{chk})")
		end if
		do me.addEvent(indent, lines, "onPush")
	end func
end class

class ObjGroup(@ObjArWnd)
	+*func draw(x: float, y: float, width: float, height: float, zoom: float)
		var text: []char :: me.props.get("text", &)
		do draw@line((x + 7.0) * zoom, (y + 6.0) * zoom, x * zoom, (y + 6.0) * zoom, 0xFFDCDCDC)
		do draw@line(x * zoom, (y + 6.0) * zoom, x * zoom, (y + height) * zoom, 0xFFDCDCDC)
		do draw@line(x * zoom, (y + height) * zoom, (x + width) * zoom, (y + height) * zoom, 0xFFDCDCDC)
		do draw@line((x + width) * zoom, (y + height) * zoom, (x + width) * zoom, (y + 6.0) * zoom, 0xFFDCDCDC)
		do draw@line((x + width) * zoom, (y + 6.0) * zoom, (x + 7.0 + \common@fontP.calcWidth(text)) * zoom, (y + 6.0) * zoom, 0xFFDCDCDC)
		do \common@fontP.drawScale((x + 7.0) * zoom, y * zoom, zoom, zoom, text, draw@black)
	end func
	
	+*func getMinMax(minWidth: &int, minHeight: &int, maxWidth: &int, maxHeight: &int)
		do minWidth :: 10
		do minHeight :: 10
		do maxWidth :: lib@intMax
		do maxHeight :: lib@intMax
	end func
	
	+*func getDefaultSize(width: &int, height: &int)
		do width :: 200
		do height :: 100
	end func
	
	+*func kind(): []char
		ret "Group"
	end func
	
	+*func icon(): int
		ret 4
	end func
	
	+*func init(name: []char, visible: bool, x: int, y: int, width: int, height: int, props: dict<[]char, []char>)
		do super(me, name, visible, x, y, width, height, props)
		if(props <>& null)
			ret
		end if
		do me.props.add("anchorX", "%fix")
		do me.props.add("anchorY", "%fix")
		do me.props.add("text", me.name)
		do me.reloadRes()
	end func
	
	+*func becomeParent(): bool
		ret true
	end func
	
	+*func propsNames(): [][]char
		ret["anchorX", "anchorY", "text"]
	end func
	
	+*func propsTypes(): []\doc_ar@PropType
		ret[%enum_ $ \doc_ar@PropType, %enum_, %str]
	end func
	
	+*func propsTypeDatas(): [][][]char
		ret[["%fix", "%move", "%scale"], ["%fix", "%move", "%scale"], null]
	end func
	
	+*func type(): []char
		ret "wnd@Group"
	end func
	
	+*func getSrcMake(indent: int, lines: list<[]char>, parent: []char)
		do lines.add("\t".repeat(indent) ~ "do @\{me.name} :: wnd@makeGroup(\{parent}, \{me.x}, \{me.y}, \{me.width}, \{me.height}, \{me.props.get("anchorX", &)}, \{me.props.get("anchorY", &)}, \"\{me.props.get("text", &)}\")")
	end func
end class
