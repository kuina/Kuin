; Network Sample (C)Kuina-chan

const url: []char :: "https://kuina.ch"

func main()
	var wndMain: wnd@Wnd :: wnd@makeWnd(null, %normal, 1024, 768, "Network Sample")
	var edit1: wnd@EditMulti :: wnd@makeEditMulti(wndMain, 12, 12, 485, 676, %fix, %scale)
	do edit1.readonly(true)
	var edit2: wnd@EditMulti :: wnd@makeEditMulti(wndMain, 500, 12, 485, 676, %scale, %scale)
	do edit2.readonly(true)
	
	; HTTPでHTMLデータを取得する
	var http: net@Http :: net@makeHttp(@url, false, null)
	var s: []char :: null
	while(s =& null) {取得に成功するまでループ}
		do lib@sleep(1) {CPUを占有しないようにする}
		do s :: http.get()
	end while
	do edit1.setText(s ~ "\n")
	
	; TCP通信を行う
	var testData: []bit8 :: "Test Data" $> []bit8 {送信するデータ}
	var server: net@TcpServer :: net@makeTcpServer(1024) {サーバの作成}
	var client: net@Tcp :: net@makeTcpClient("localhost", 1024) {クライアントを作成してサーバに接続}
	var serverConnect: net@Tcp :: null
	while(serverConnect =& null) {クライアントからの接続要求があるまでループ}
		do lib@sleep(1)
		do serverConnect :: server.get()
	end while
	do client.send(testData) {クライアントからデータを送信}
	var serverData: []bit8 :: null
	while(serverData =& null) {クライアントが送信したデータが受信できるまでループ}
		do lib@sleep(1)
		do serverData :: serverConnect.receive(^testData)
	end while
	do edit2.setText((serverData $< []char) ~ "\n")
	
	while(wnd@act())
	end while
end func
